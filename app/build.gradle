apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId 'com.nodrev.bgfx.examples'
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 100 // Application version, 3 digits, major/minor/revision
        versionName "1.0.0"
    }
    // One APK per ABI. Generate also one multi-abi APK.
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'x86'
            universalApk true
        }
    }
    // Helper function to generate a unique version code per application version and per abi
    // Method comes from https://androidbycode.wordpress.com/2015/07/07/android-ndk-a-guide-to-deploying-apps-with-native-libraries/
    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 0) * 10000000 + android.defaultConfig.versionCode
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
    }
    productFlavors {
    }
    lintOptions {
        // I have a critical lint error when using a compileSdkVersion less than 26 with last
        // build tools. I have an old phone, so need to ignore this error.
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:23.4.0'
}
